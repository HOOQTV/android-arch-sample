apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.imadji.arch.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionProperties.code
        versionName versionProperties.name
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile file('../keystore/debug.keystore')
        }
        release {
            keyAlias 'archsamplekey'
            keyPassword keystoreProperties.keyPassword
            storePassword keystoreProperties.storePassword
            storeFile file('../keystore/release.keystore')
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard-debug.pro"
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$project.rootDir/tools/rules-proguard.pro"
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "Arch Sample Dev"
        }
        prod {
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Arch Sample"
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                jvmArgs '-noverify'
            }
        }
    }

    android.applicationVariants.all { variant ->
        def apkName = "arch-sample"
        apkName += "-" + variant.versionName
        variant.outputs.all {
            outputFileName = apkName + ".apk"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.testDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')
    implementation project(':data')

    implementation appDependencies.appCompat
    implementation appDependencies.design
    implementation appDependencies.legacySupport
    implementation appDependencies.recyclerView
    implementation appDependencies.constraintLayout

    /* Android Architecture Components */
    implementation appDependencies.lifecycleExtension
    implementation appDependencies.lifecycleCommon

    /* Butterknife for inject Views */
    implementation appDependencies.butterknife
    annotationProcessor appDependencies.butterknifeCompiler

    /* Glide for Image Loading */
    implementation appDependencies.glide
    annotationProcessor appDependencies.glideCompiler

    /* Firebase */
    implementation appDependencies.firebaseCore

    /* RxJava RxAndroid */
    implementation appDependencies.rxJava
    implementation appDependencies.rxAndroid

    /* Retrofit */
    implementation appDependencies.retrofit

    /* Dagger for Dependency Injection */
    implementation appDependencies.dagger
    annotationProcessor appDependencies.daggerCompiler

    /* Test Libs */
    testImplementation testDependencies.jUnit
    testImplementation testDependencies.mockitoCore
    testImplementation testDependencies.robolectric
    testImplementation testDependencies.lifecycleTest
    androidTestImplementation testDependencies.androidTestRunner
    androidTestImplementation testDependencies.mockitoAndroid
    androidTestImplementation testDependencies.espressoCore
}

apply plugin: 'com.google.gms.google-services'
